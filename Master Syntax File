
#MASTER SYNTAX FILE FOR SPSS CODE FOR CONSTRUCTING VARIABLES FOR IIFG PROJECT.

COMMENT CONSTRUCT THE RACE VARIABLE.
COMMENT THIS APPROACH ASSIGNS RACE FROM MOST NUMEROUS TO LEAST NUMEROUS CATEGORY.
COMMENT WHITE HISPANIC ASIAN BLACK NATIVE AMERICAN THEN OTHER.
COMMENT LESS NUMEROUS CATEGORIES OVERWRITE MORE NUMEROUS CATEGORIES WHERE MULTIPLE OPTIONS ARE CHECKED.
COMMENT THIS MAXIMIZES SIZE OF RACIAL CATEGORIES FOR ANALYSIS.
COMMENT OTHER IS ALLOWED TO OVERWRITE ALL CATEGORIES THAT PRECEED IT.

USE all.

DO IF q74e = 1.
    COMPUTE RACE = 5.
END IF.

DO IF q74b = 1.
    COMPUTE RACE = 2.
END IF.

DO IF q74d = 1.
    COMPUTE RACE = 4.
END IF.

DO IF q74a = 1 .
    COMPUTE RACE = 1.
END IF.

DO IF q74c = 1.
    COMPUTE RACE = 3.
END IF.

DO IF q74f = 1.
    COMPUTE RACE = 6.
END IF.

FREQUENCIES VARIABLES = RACE q74b q74c q74d q74e q74f 
   /ORDER=ANALYSIS.

COMMENT CONSTRUCT THE RACE2 VARIABLE.
COMMENT THIS APPROACH ASSIGNS RACE FROM MOST NUMEROUS TO LEAST NUMEROUS CATEGORY.
COMMENT OTHER WHITE HISPANIC ASIAN BLACK NATIVE AMERICAN.
COMMENT LESS NUMEROUS CATEGORIES OVERWRITE MORE NUMEROUS CATEGORIES WHERE MULTIPLE OPTIONS ARE CHECKED.
COMMENT THIS MAXIMIZES SIZE OF RACIAL CATEGORIES FOR ANALYSIS.
COMMENT BECAUSE OTHER PRECEDES ALL CATEGORIES MORE SPECIFIC CATEGORIES OVERWRITE IT.

USE all.

DO IF q74f = 1.
    COMPUTE RACE2 = 6.
END IF.

DO IF q74e = 1.
    COMPUTE RACE2 = 5.
END IF.

DO IF q74b = 1.
    COMPUTE RACE2 = 2.
END IF.

DO IF q74d = 1.
    COMPUTE RACE2 = 4.
END IF.

DO IF q74a = 1 .
    COMPUTE RACE2 = 1.
END IF.

DO IF q74c = 1.
    COMPUTE RACE2 = 3.
END IF.

COMMENT ADD VALUE LABELS TO RACE AND RACE2.

VALUE LABELS RACE RACE2 
    1 "BLACK" 
    2 "HISPANIC/LATINO" 
    3 "NATIVE AMERICAN" 
    4 "ASIAN/PACIFIC ISLANDER"
    5 "WHITE"
    6 "OTHER".

COMMENT NEW BLOCK OF VARIABLES.
COMMENT CREATE DEMOGRAPHIC VARIABLES.

COMMENT CREATE A BINARY GENDER VARIABLE, LABEL VALUES.

RECODE q73 (1=0) (2=1) (ELSE=SYSMIS) INTO GENDER.
VARIABLE LABELS  GENDER 'Constructed male female binary variable'.
EXECUTE.

VALUE LABELS GENDER
    0 "MALE"
    1 "FEMALE".
EXECUTE.

COMMENT CREATE VARIABLE MARRIED DURING LAW SCHOOL.

RECODE q84ab (1=1) (2=0) (ELSE=SYSMIS) INTO MARRIED_IN_LS.
VARIABLE LABELS  MARRIED_IN_LS 'R was married during law school.'.
EXECUTE.

COMMENT CREATE VARIABLE CHILDREN UNDER 18 LIVING WITH YOU DURING LAW SCHOOL.

RECODE q84bb (1=1) (2=0) (ELSE=SYSMIS) INTO KIDS_IN_LS.
VARIABLE LABELS  KIDS_IN_LS 'Children under 18 living with R during law school'.
EXECUTE.

COMMENT CREATE VARIABLE MARRIED DURING JOB 1.

RECODE q84ac (1=1) (2=0) (ELSE=SYSMIS) INTO MARRIED_JOB1.
VARIABLE LABELS  MARRIED_JOB1  'R was married during job 1.'.
EXECUTE.

COMMENT CREATE VARIABLE CHILDREN UNDER 18 LIVING WITH YOU DURING JOB 1.

RECODE q84bc (1=1) (2=0) (ELSE=SYSMIS) INTO KIDS_JOB1.
VARIABLE LABELS  KIDS_JOB1 'Children under 18 living with R during job 1'.
EXECUTE.

COMMENT ADD VALUE LABELS FOR FAMILY STATUS VARIABLES..

VALUE LABLES MARRIED_IN_LS KIDS_IN_LS MARRIED_JOB1 KIDS_JOB1
    1 "YES"
    0 "NO".
EXECUTE.


COMMENT NEW BLOCK OF VARIABLES.
COMMENT CREATE FIRST GENERATION JD VARIABLES.

COMMENT CREATE VARIABLE FOR MOTHER OR GUARDIAN 1 COMPLETED JD.

RECODE q78a (8=1) (9 thru 10=0) (1 thru 7=0) (ELSE=SYSMIS) INTO MOTHER_JD.
VARIABLE LABELS  MOTHER_JD 'Mother or guardian 1 completed JD.'.
EXECUTE.

COMMENT CREATE VARIABLE FOR FATHER OR GUARDIAN 2 COMPLETE JD.

RECODE q78b (8=1) (9 thru 10=0) (1 thru 7=0) (ELSE=SYSMIS) INTO FATHER_JD.
VARIABLE LABELS  FATHER_JD 'Father or guardian 2 completed JD.'.
EXECUTE.

COMMENT CREATE VARIABLE FOR CLOSE RELATIVE WAS A LAWYER.

RECODE q79 (1=1) (2=0) (ELSE=SYSMIS) INTO CLOSE_REL_JD.
VARIABLE LABELS  CLOSE_REL_JD 'Close relative was a lawyer.'.
EXECUTE.

COMMENT CREATE VARIABLE FOR FIRST GENERATION JD STUDENT INCLUDING RELATIVES.

DO IF (MOTHER_JD EQ 0 AND FATHER_JD EQ 0 AND CLOSE_REL_JD EQ 0).
    COMPUTE FIRST_GEN_JD_ALL =1.
        ELSE IF (MOTHER_JD EQ 1 OR FATHER_JD EQ 1 OR CLOSE_REL_JD EQ 1).
        COMPUTE FIRST_GEN_JD_ALL = 0.
END IF.        

COMMENT CREATE VARIABLE FOR FIRST GENERATION JD STUDENT ONLY PARENTS.

DO IF (MOTHER_JD EQ 0 AND FATHER_JD EQ 0).
    COMPUTE FIRST_GEN_JD =1.
        ELSE IF (MOTHER_JD EQ 1 OR FATHER_JD EQ 1).
        COMPUTE FIRST_GEN_JD = 0.
END IF.        


COMMENT ADD VALUE LABELS FOR VARIABLES ABOUT FAMILY MEMBERS WHO ARE JDS.

VALUE LABELS MOTHER_JD FATHER_JD CLOSE_REL_JD FIRST_GEN_JD_ALL FIRST_GEN_JD 
    1 "YES"
    0 "NO".

COMMENT NEW BLOCK OF VARIABLES.
COMMENT CONSTRUCT DEPENDENT MEASURES.

COMMENT CREATE VARIABLE R DOES PRO BONO IN CURRENT JOB.

RECODE q64 (1=1) (2=0) (ELSE=SYSMIS) INTO PRO_BONO_NOW.
VARIABLE LABELS  PRO_BONO_NOW 'R does pro bono in current job.'.
EXECUTE.

COMMENT CREATE VARIABLES ABOUT CIVIC ENGAGEMENT FROM Q68.

COMMENT CREATE VARIABLE ABOUT SERVING ON A BAR COMMITTEE.
RECODE q68b (1=1) (MISSING=0) INTO BAR_COM.
VARIABLE LABELS  BAR_COM 'R served on a bar committee in past year.'.
EXECUTE.

COMMENT CREATE VARIABLE ABOUT VOLUNTEERING FOR A POLITICAL CAMPAIGN.
RECODE q68c (1=1) (MISSING=0) INTO VOL_POLITICS.
VARIABLE LABELS  VOL_POLITICS 'R volunteered for a political campaign in past year.'.
EXECUTE.

COMMENT CREATE VARIABLE ABOUT SERVING ON BOARD OF NONPROFIT.
RECODE q68d (1=1) (MISSING=0) INTO NONPROFIT_BD.
VARIABLE LABELS  NONPROFIT_BD 'R served on the board of a nonprofit org in past year.'.
EXECUTE.

COMMENT CREATE VARIABLE ABOUT VOLUNTEERING ON A COMMUNITY PROJECT.
RECODE q68e (1=1) (MISSING=0) INTO COM_PROJECT.
VARIABLE LABELS  COM_PROJECT 'R volunteered on a community project in past year.'.
EXECUTE.

COMMENT CREATE VARIABLE ABOUT VOLUNTEERING ON A PROJECT TO HELP THOSE IN NEED.
RECODE q68f (1=1) (MISSING=0) INTO NEED_PROJECT.
VARIABLE LABELS  NEED_PROJECT 'R volunteered on a project to help those in need in past year.'.
EXECUTE.

COMMENT CREATE VARIABLE ABOUT VOLUNTEERING ON A PROJECT TO ADVANCE A CAUSE.
RECODE q68f (1=1) (MISSING=0) INTO CAUSE_PROJECT.
VARIABLE LABELS  CAUSE_PROJECT 'R volunteered on a project to advance a cause in past year.'.
EXECUTE.

COMMENT CREATE BINARY VARIABLE INDICATING WHETHER R GAVE MONEY TO PUBLIC INTEREST LAW ORG IN LAST YEAR.
RECODE q68h (1=1) (MISSING=0) INTO GAVE_PILOS.
VARIABLE LABELS  GAVE_PILOS '"R gave money to public interest law organizations in past year.'.
EXECUTE.

COMMENT CREATE VARIABLE AMOUNT OF CONTRIBUTION TO PUBLIC INTEREST LAW ORGANIZATIONS.
COMMENT INFORMATION IN Q69 WAS IN STRING FORM, THIS CONVERTS IT TO NUMERIC.
COMMENT RECODE STATEMENT CONVERTS NUMERIC VALUES FOR MISSING DATA INTO SYSMIS VALUES.

COMPUTE AMT$_TO_PILOS = NUMBER (q69, F20).
VARIABLE LABELS AMT$_TO_PILOS 'Dollar amount R gave to public interest law organizations in the last year'.
EXECUTE.

RECODE AMT$_TO_PILOS (-8=SYSMIS) (-9=SYSMIS).
EXECUTE.

COMMENT CREATE VALUE LABELS FOR PRO_BONO_NOW AND VARIABLES FROM Q68.
COMMENT ASSIGN VALUE LABELS TO THESE YES NO VARIABLES.
COMMENT ACTUAL VALUES REMAIN NUMERIC FOR COMPUTATIONAL PURPOSES EG LOGISTIC REGRESSION.
VALUE LABELS 
PRO_BONO_NOW GAVE_PILOS BAR_COM VOL_POLITICS NONPROFIT_BD COM_PROJECT NEED_PROJECT CAUSE_PROJECT
    1 "YES"
    0 "NO".

COMMENT CREATE JOB SETTING VARIABLES FOR PUBIC INTEREST PRACTICE.
COMMENT q63_job1a IS THE CURRENT JOB.
COMMENT q63_job1b IS THE FIRST JOB.
COMMENT CATEGORY GROUPINGS ARE BASED ON THE JOB GROUP LABELS FROM THE JOB SETTINGS DROP DOWN MENU.

RECODE q63_job1a (MISSING=SYSMIS) (1 thru 12=1) (13 thru 24=2) (25 thru 29=3) (30 thru 32=4) (33 
    thru 35=5) (36 thru 38=6) INTO JOB_SET_CURRENT.
VARIABLE LABELS  JOB_SET_CURRENT 'Job setting for job R had at time of survey.'.
EXECUTE.

RECODE q63_job2a (MISSING=SYSMIS) (1 thru 12=1) (13 thru 24=2) (25 thru 29=3) (30 thru 32=4) (33 
    thru 35=5) (36 thru 38=6) INTO JOB_SET_FIRST_JOB.
VARIABLE LABELS  JOB_SET_FIRST_JOB 'Job setting for first job R held.'.
EXECUTE.

VALUE LABELS JOB_SET_CURRENT JOB_SET_FIRST_JOB
    1 "PRIVATE PRACTICE"
    2 "GOVERNMENT"
    3 "NONPROFIT"
    4 "PUBLIC DEFENDER"
    5 "PRIVATE COMPANY"
    6 "MILITARY".

COMMENT CONSTRUCT JOB SETTING VARIABLES FOLDING MILITARY INTO GOVERNMENT SETTING BECAUSE MILITARY HAS VERY SMALL N < 10.
COMMENT SAME CODE AS ABOVE WITH THE EXCEPTION THAT MILTIARY AND GOVERNMENT JOBS ARE BOTH CODED AS 2.
COMMENT VARIABLE NAMES ARE THE SAME EXCEPT 2 APPENDED AT THE END TO INDICATE ALTERNATE CODING FOR SETTING.

RECODE q63_job1a (MISSING=SYSMIS) (1 thru 12=1) (13 thru 24=2) (25 thru 29=3) (30 thru 32=4) (33 
    thru 35=5) (36 thru 38=2) INTO JOB_SET_CURRENT2.
VARIABLE LABELS  JOB_SET_CURRENT2 'Job setting for job R had at time of survey, military setting included in government.'.
EXECUTE.

RECODE q63_job2a (MISSING=SYSMIS) (1 thru 12=1) (13 thru 24=2) (25 thru 29=3) (30 thru 32=4) (33 
    thru 35=5) (36 thru 38=2) INTO JOB_SET_FIRST_JOB2.
VARIABLE LABELS  JOB_SET_FIRST_JOB2 'Job setting for first job R held, military setting included in goverment.'.
EXECUTE.

VALUE LABELS JOB_SET_CURRENT2 JOB_SET_FIRST_JOB2
    1 "PRIVATE PRACTICE"
    2 "GOVERNMENT"
    3 "NONPROFIT"
    4 "PUBLIC DEFENDER"
    5 "PRIVATE COMPANY".
    
COMMENT CONSTRUCT MEASURES OF PUBLIC INTEREST JOBS.
COMMENT THESE MEASURES ARE CONSTRUCTED USING THE SETTINGS VARIABLES THAT FOLD MILITARY INTO GOVERNMENT.
COMMENT FIRST I CONSTRUCT VARIABLES FOR A NARROW DEFINITION OF PUBLIC INTEREST JOB LIMITED TO NONPROFIT SETTINGS.

DO IF (JOB_SET_CURRENT2 = 3).
    COMPUTE CURRENT_JOB_PIA = 1.
    ELSE IF (JOB_SET_CURRENT2 EQ 1 OR JOB_SET_CURRENT2 = 2 OR JOB_SET_CURRENT2 = 4 OR JOB_SET_CURRENT2 = 5).
    COMPUTE CURRENT_JOB_PIA = 0.
END IF.
VARIABLE LABELS  CURRENT_JOB_PIA 'Current job is public interest = nonprofit only.'.
EXECUTE.

DO IF (JOB_SET_FIRST_JOB2 = 3).
    COMPUTE FIRST_JOB_PIA = 1.
    ELSE IF (JOB_SET_FIRST_JOB2 EQ 1 OR JOB_SET_FIRST_JOB2 = 2 OR JOB_SET_FIRST_JOB2 = 4 OR JOB_SET_FIRST_JOB2 = 5).
    COMPUTE FIRST_JOB_PIA = 0.
END IF.
VARIABLE LABELS  FIRST_JOB_PIA 'First job was public interest = nonprofit only.'.
EXECUTE.

COMMENT CONSTRUCT VARIABLES FOR BROAD DEFINITION OF PUBLIC INTEREST JOB THAT INCLUDES NONPROFITS GOVERNMENT AND PUBLIC DEFENDERS.

DO IF (JOB_SET_CURRENT2 = 3 OR JOB_SET_CURRENT2 = 2 OR JOB_SET_CURRENT2 = 4).
    COMPUTE CURRENT_JOB_PIB = 1.
    ELSE IF (JOB_SET_CURRENT2 EQ 1 OR JOB_SET_CURRENT2 = 5).
    COMPUTE CURRENT_JOB_PIB = 0.
END IF.
VARIABLE LABELS  CURRENT_JOB_PIB 'Current job is public interest = nonprofit, government and public defender.'.
EXECUTE.

DO IF (JOB_SET_FIRST_JOB2 = 3 OR JOB_SET_FIRST_JOB2 = 2 OR JOB_SET_FIRST_JOB2 = 4).
    COMPUTE FIRST_JOB_PIB = 1.
    ELSE IF (JOB_SET_FIRST_JOB2 EQ 1 OR JOB_SET_FIRST_JOB2 = 5).
    COMPUTE FIRST_JOB_PIB = 0.
END IF.
VARIABLE LABELS  FIRST_JOB_PIB 'First job was public interest = nonprofit, government and public defender.'.
EXECUTE.

COMMENT ASSIGN VALUE LABELS TO YES NO VARIABLES.
COMMENT ACTUAL VALUES REMAIN NUMERIC FOR COMPUTATIONAL PURPOSES EG LOGISTIC REGRESSION.

VALUE LABELS CURRENT_JOB_PIA CURRENT_JOB_PIB FIRST_JOB_PIA FIRST_JOB_PIB
    1 "YES"
    0 "NO".




